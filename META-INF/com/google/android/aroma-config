### LICENSE:
#
# Copyright (C) 2011 Ahmad Amarullah ( http://amarullz.com/ )
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
### FILE INFO:
#
# AROMA Installer 2.70 - AROMA Installer Test Script Configuration
#       (c) 2011-2012 by Ahmad Amarullah
#           amarullz - xda-developers
#           http://www.amarullz.com/
#
#       Binary Version  : 2.70B
#       Binary Codename : Flamboyan
#
### INFO:
#
# * AROMA Resource Dir  = META-INF/com/google/android/aroma
#
# * AROMA Temporary Dir = /tmp/aroma/
#
# * About Icon          = @alert - show <AROMA Resource Dir>/icons/alert.png
#                         or if theme used it will find <themedir>/icon.alert.png
#                         you can still access it via "icons/alert"
#
# * dp                  = Device Pixelate. WVGA/QHD = 3, HVGA = 2.
#                         dp calculated with = round(min(width,height) / 160)
#
###

##
# Fix Colorspace Issue
#
# For device which have a problem with color ( not display the correct color )
# try to set "force_colorspace" with ini_set. It may caused by wrong framebuffer
# information that passed by kernel into framebuffer device.
#
# You can use one of following values:
#   "rgba", "abgr", "argb", "bgra"
#
# NOTE: It must be in top most script, or you will got the color flicker
#
#ini_set("force_colorspace","argb");
#

##
# Forcing Device Pixelate Size
#  defaut: floor( min( width, height ) / 160 )
#
# Usefull for tablet device, if interface was too big, you can
# force dp size for smaller UI size.
#
#-------- Uncomment one of this to use custom dp size:
#
# ini_set("dp","1"); #-- LDPI ( 240x320 / QVGA )
# ini_set("dp","2"); #-- MDPI ( 340x480 / HVGA )
# ini_set("dp","3"); #-- HDPI ( 480x800 / WVGA )
# ini_set("dp","4");
# ini_set("dp","5");
#

##
# Custom Key Code - Usefull For Some Device which not used
#                   Standard key code for up/down/select/back/menu
#
# You can capture the keycode in
# recovery using "aroma-keycapture-flashable.zip" Available in
# AROMA Installer Thread.
#
# Press some key when it run, it will return key code,
# that key can override up/down/select/back/menu actions in
# AROMA Installer, you can even set your home key for up, or
# volume down for select item
#
#-------- Uncomment & Modify this Code:
#
# ini_set("customkeycode_up",     "115");
# ini_set("customkeycode_down",   "114");
# ini_set("customkeycode_select", "116");
# ini_set("customkeycode_menu",   "229");
# ini_set("customkeycode_back",   "158");
#

##
# Initializing Rom Information
#
ini_set("rom_name",             "ImproveG3");
ini_set("rom_version",          "0.5");
ini_set("rom_author",           "subworx");
ini_set("rom_device",           "LG G3 D855");
ini_set("rom_date",             "2014.12.01");


#
# Set Small Font to Support all fonts
#
fontresload( "0", "ttf/Roboto-Regular.ttf", "12" ); #-- Use sets of font (Font Family)
fontresload( "1", "ttf/Roboto-Regular.ttf", "14" ); #-- Use sets of font (Font Family)

#
# INCLUDE EXAMPLE = Language Selection & Form Example
#
include("language_select.edify");
#include("custombox_test.edify");

#########################################################################################################
#
# STEP 1 - SHOW ROM INFORMATION
#
viewbox(
  #-- Title
    "<~welcome.title>",
  
  #-- Text
    "<~welcome.text1> <b>"+
	  #-- Get Config Value
	  ini_get("rom_name")+
	"</b> <~common.for> <b>"+ini_get("rom_device")+"</b>.\n\n"+
    
    "<~welcome.text2>\n\n"+
	
      "  <~welcome.version>\t\t: <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
      "  <~welcome.updated>\t\t: <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n"+
	  "  <~welcome.creator>\t\t: <b><#selectbg_g>"+ini_get("rom_author")+"</#></b>\n\n\n"+
	  
    "<~welcome.next>",

  #-- Icon
    "@welcome"
);

#########################################################################################################
#
# STEP 2 - LICENSE
#
agreebox(
  #-- Title
    "<~terms.title>",
  
  #-- Subtitle / Description
    "<~terms.desc>",
  
  #-- Icon
    "@license",
  
  #-- Text Content ( Read from <AROMA Resource Dir>/license.txt )
    resread("license.txt"),
  
  #-- Checkbox Text
    "<~terms.check>",
  
  #-- Unchecked Alert Message
    "<~terms.confirm>"
);


#########################################################################################################
#
# STEP 3 - CHANGELOG
#
textbox(
  #-- Title
    "<~changelog.title>",
  
  #-- Subtitle
    "<~changelog.desc>",
  
  #-- Icon
    "@update",
  
  #-- Arg 4
    resread("changelog.txt");
);

##
##-- Use Roboto-Regular.ttf
##
fontresload( "0", "ttf/Roboto-Regular.ttf", "12" );
fontresload( "1", "ttf/Roboto-Regular.ttf", "14" );
##
##


#
# Save Demo Menu Position
#
gotolabel("aroma_installer_demo_pos");

#########################################################################################################
#
# STEP 4 - SHOW MENUBOX - AROMA INSTALLER DEMO MENU
#
menubox(
  #-- Title
    "<~mainmenu.title>",
  
  #-- Sub Title
    "<~mainmenu.desc>",
  
  #-- Icon
    "@apps",
    
  #-- Will be saved in /tmp/aroma/demo.prop
    "demo.prop",
    
  #-------------------------+-----------[ Menubox Items ]-------------------+---------------#
  # TITLE                   |  SUBTITLE                                     |   Item Icons  #
  #-------------------------+-----------------------------------------------+---------------#
  
    "<~mainmenu.removeapps>", "<~mainmenu.removedesc>",                      "@customize",   #-- selected = 1
	"<~mainmenu.sdfix>",	  "<~mainmenu.sdfixdesc>",						 "@customize",	 #-- selected = 2
	#"<~mainmenu.cttmod>",     "<~mainmenu.cttdesc>", 						 "@customize",   #-- selected = 
	#"<~mainmenu.sharp>",	  "<~mainmenu.sharpdesc>",						 "@customize",	 #-- selected = 
	"<~mainmenu.systeminfo>", "<~mainmenu.systemdesc>",         			 "@info"         #-- selected = 3
);


#########################################################################################################
#                                                                                                       #
#                                        STEP 5 - ACTIONS                                               #
#                                                                                                       #
#########################################################################################################

##--------------------------------------
#
# System Info Demo / selected 5
#
if prop("demo.prop","selected")=="3" then

  #-- Show Please Wait
  pleasewait("Get System Informations...");

  #-- Fetch System Information
  setvar(
    #-- Variable Name
      "sysinfo",
    
    #-- Variable Value
      "<@center><b>Your Device System Information</b></@>\n\n"+
      
      "Model\t\t\t: <b><#selectbg_g>"+sysprop("ro.product.model")+"</#></b>\n"+
      "Name\t\t\t: <b><#selectbg_g>"+sysprop("ro.product.name")+"</#></b>\n"+
      "Device Name\t\t: <b><#selectbg_g>"+sysprop("ro.product.device")+"</#></b>\n"+
      "Board Name\t\t: <b><#selectbg_g>"+sysprop("ro.product.board")+"</#></b>\n"+
      "Manufacturer\t\t: <b><#selectbg_g>"+sysprop("ro.product.manufacturer")+"</#></b>\n\n"+
      
      "CPU\t\t\t: <b><#selectbg_g>"+sysprop("ro.product.cpu.abi")+"</#></b>\n"+
      "LCD Desinty\t\t: <b><#selectbg_g>"+sysprop("ro.sf.lcd_density")+"</#></b>\n\n"+
      
      "System Size\t\t: <b><#selectbg_g>"+getdisksize("/system","m")+" MB</#></b>\n"+
        "\tFree\t\t: <b><#selectbg_g>"+getdiskfree("/system","m")+" MB</#></b>\n\n"+
      "Data Size\t\t: <b><#selectbg_g>"+getdisksize("/data","m")+" MB</#></b>\n"+
        "\tFree\t\t: <b><#selectbg_g>"+getdiskfree("/data","m")+" MB</#></b>\n\n"+
      "(/mnt/sdcard)\t\t: <b><#selectbg_g>"+getdisksize("/mnt/sdcard","m")+" MB</#></b>\n"+
        "\tFree\t\t: <b><#selectbg_g>"+getdiskfree("/mnt/sdcard","m")+" MB</#></b>\n\n"+
      "SDCard Size\t\t: <b><#selectbg_g>"+getdisksize("/sdcard","m")+" MB</#></b>\n"+
        "\tFree\t\t: <b><#selectbg_g>"+getdiskfree("/sdcard","m")+" MB</#></b>\n\n"+
      "SD-EXT Size\t\t: <b><#selectbg_g>"+getdisksize("/sd-ext","m")+" MB</#></b>\n"+
        "\tFree\t\t: <b><#selectbg_g>"+getdiskfree("/sd-ext","m")+" MB</#></b>\n\n"+
	  "(/cust)\t\t: <b><#selectbg_g>"+getdisksize("/cust","m")+" MB</#></b>\n"+
        "\tFree\t\t: <b><#selectbg_g>"+getdiskfree("/cust","m")+" MB</#></b>\n\n"+
      

      ""
  );
  
  #-- Show Textbox
  textbox(
    #-- Title
      "System Information",
    
    #-- Subtitle
      "Current system information may not be supported in all devices.",
    
    #-- Icon
      "@info",
    
    #-- Text
      getvar("sysinfo")
  );
  
  #-- Show Alert
  alert(
    #-- Alert Title
      "Finished",
    
    #-- Alert Text
      "Returning to Main Menu",
    
    #-- Alert Icon
      "@alert"
  );
  
  #-- Back to Demo Menu ( 2 Wizard UI to Back )
  back("2");
  
endif;

##--------------------------------------
#
# Oversharpening Fix / selected 4
#
#if prop("demo.prop","selected")=="4" then
#  include("sharpening.edify");
#endif;

##--------------------------------------
#
# Temperature Mod / selected 3
#
#if prop("demo.prop","selected")=="3" then
#  include("temperature.edify");
#endif;

##--------------------------------------
#
# SD Fix / selected 2
#
if prop("demo.prop","selected")=="2" then
  #########################################################################################################
  #                                                                                                       #
  #                                             SD FIX                                                    #
  #                                                                                                       #
  #########################################################################################################

  #########################################################################################################
  #
  # SD FIX STEP 1 - INSTRUCTIONS
  #
  textbox(
    #-- Title
      "<~sdfixinstructions.title>",
  
    #-- Subtitle
      "<~sdfixinstructions.desc>",
  
    #-- Icon
      "@alert",
  
    #-- Arg 4
      "<~sdfixinstructions.warning>"
  );

  #########################################################################################################
  #
  # SD FIX STEP 2 - SELECT ACTION
  #
  selectbox(
  #-- Title
    "<~sdfixcustomize.title>",
  
  #-- Sub Title
    "<~sdfixcustomize.desc>",
  
  #-- Icon: <AROMA Resource Dir>/icons/personalize.png or <ThemeDir>/icon.personalize.png
    "@personalize",
  
  #-- Will be saved in /tmp/aroma/sdfix.prop
    "sdfix.prop",
  
  #-------------------------[ Selectbox Without Group ]--------------------------#
  # TITLE                 |  SUBTITLE                            | Initial Value #
  #-----------------------+--------------------------------------+---------------#

    "<~sdfixcustomize.1>", "<~sdfixcustomize.1desc>",              1,      #-- selected.0 = 1
    "<~sdfixcustomize.2>", "<~sdfixcustomize.2desc>",              0       #-- selected.0 = 2

  #--------[ Initial Value = 0: Unselected, 1: Selected, 2: Group Item, 3: Not Visible ]---------#
  );
  
  ##
  #
  # Next Screen is Installation UI, So change the next button text to "Install Now"
  #
  ini_set("text_next", "<~sdfixcustomize.nextbutton>");
  ini_set("icon_next", "@installbutton");

  ###################################################################################################################
  #
  # SD FIX STEP 3 - MAKE SURE USER KNOW THAT NEXT STEP WAS INSTALLATION PROCESS
  #
  viewbox(
    #-- Title
      "<~sdfixconfirmation.title>",

    #-- Text
      "<~sdfixconfirmation.text>",

    #-- Icon
      "@install"
  );

  ##
  #
  # Next Button in successfull installation should be back to "Next >"
  #
  ini_set("text_next", "<~text_next>");
  ini_set("icon_next", "@next");

  #-- Extract sleep to /tmp/aroma/sleep. It will be used in updater-script for testing wait script
  restotmp("exec_demo/sleep","sleep");

  ###################################################################################################################
  #
  # SD FIX STEP 4 - INSTALLATION PROCESS
  #
  setvar("retstatus",
    install(
	  #-- Title
        "<~sdfixinstall.title>",
	
	  #-- Installation Process message
	    "<b>"+ini_get("rom_name")+"</b> <~sdfixinstall.desc>",
	
	  #-- Installation Icon
        "@install",
	
	  #-- Installation Finish Message
        "<~sdfixinstall.finish>"
    )
  );

  ###################################################################################################################
  #
  # SD FIX STEP 5 - ASK If We Want to go back to main menu
  #
  checkviewbox(
    #-- Title
      "<~sdfixpostinstall.title>",
  
    #-- Text
      "<~sdfixpostinstall.text>",
	
    #-- Icon
      "@welcome",

    #-- Checkbox Text
      "<~sdfixpostinstall.checkbox>",

    #-- Initial Checkbox value ( 0=unchecked, 1=checked ) -  (Optional, default:0)
      "0",

    #-- Save checked value in variable "reboot_it" (Optional)
      "goto_mainmenu"
  );
  if
    getvar("goto_mainmenu")=="1"
  then
    goto("aroma_installer_demo_pos");
  endif;
  
  ##########
  #
  # END SD FIX
  #
 
endif;

##--------------------------------------
#
# Debloater / selected 1 - if necessary?
#
if prop("demo.prop","selected")=="1" then
  #########################################################################################################
  #                                                                                                       #
  #                                           DEBLOATER                                                   #
  #                                                                                                       #
  #########################################################################################################

  #########################################################################################################
  #
  # DEBLOATER STEP 1 - INSTRUCTIONS
  #
  textbox(
    #-- Title
      "<~debloatinstructions.title>",
  
    #-- Subtitle
      "<~debloatinstructions.desc>",
  
    #-- Icon
      "@alert",
  
    #-- Arg 4
      "<~debloatinstructions.warning>"
  );

  #########################################################################################################
  #
  # DEBLOATER STEP 2 - CUSTOMIZE
  #
  checkbox(
  #-- Title
    "<~debloatcustomize.title>",
  
  #-- Sub Title
    "<~debloatcustomize.desc>",
  
  #-- Icon: <AROMA Resource Dir>/icons/personalize.png or <ThemeDir>/icon.personalize.png
    "@personalize",
  
  #-- Will be saved in /tmp/aroma/customize.prop
    "customize.prop",
  
    #----------------------------[ Checkbox With Group ]------------------------------------#
    # TITLE               | SUBTITLE                                        | Initial Value #
    #---------------------+-------------------------------------------------+---------------#

    "<#0f0><~debloatcustomize.10></#>", "", 					                2,  #-- Group 1. key = "item.1.x"
	  "Accessibility",      "<~debloatcustomize.11>",                         0,    #-- item.1.1 = Un-Selected by default
      "Answer me",          "<~debloatcustomize.12>",         				0,    #-- item.1.2 = Un-Selected by default
	  "EasyHome",           "<~debloatcustomize.13>",      					0,    #-- item.1.3 = Un-Selected by default
	  "E-Mail",             "<~debloatcustomize.14>",                         0,    #-- item.1.4 = Un-Selected by default
	  "Face Unlock",        "<~debloatcustomize.15>",                         0,    #-- item.1.5 = Un-Selected by default
	  "File Manager",       "<~debloatcustomize.16>",                			0,    #-- item.1.6 = Un-Selected by default
	  "Home selector",	  "<~debloatcustomize.17>",                         0,    #-- item.1.7 = Un-Selected by default
	  "LG Backup",		  "<~debloatcustomize.18>",    						0,    #-- item.1.8 = Un-Selected by default
	  "LG Health",		  "<~debloatcustomize.19>",				   			0,	  #-- item.1.9 = Un-Selected by default
	  "LG Keyboard Black Theme", "<~debloatcustomize.110>",					0,	  #-- item.1.10 = Un-Selected by default
	  "LGSearchWidgetProvider", "<~debloatcustomize.111>",					0,	  #-- item.1.11 = Un-Selected by default
	  "McAfee Security",    "<~debloatcustomize.112>", 						0,	  #-- item.1.12 = Un-Selected by default
	  "Music widget",		  "<~debloatcustomize.113>",                     	0,	  #-- item.1.13 = Un-Selected by default
	  "Music app",		  "<~debloatcustomize.114>",                        0,	  #-- item.1.14 = Un-Selected by default
 	  "Smart Notice",		  "<~debloatcustomize.115>",						0,    #-- item.1.15 = Un-Selected by default
 	  "Smart Tips",		  "<~debloatcustomize.116>",						0,    #-- item.1.16 = Un-Selected by default
  
    "<#0080ff><~debloatcustomize.20></#>", "", 								2,  #-- Group 2. key = "item.2.x"
	  "Home",				  "<~debloatcustomize.21>",							0,    #-- item.2.1 = Un-Selected by default
	  "Home Theme Optimus", "<~debloatcustomize.22>",							0,    #-- item.2.2 = Un-Selected by default
	  "Internet",           "<~debloatcustomize.23>",               			0,    #-- item.2.3 = Un-Selected by default
	  "LG Keyboard",        "<~debloatcustomize.24>",   						0,    #-- item.2.4 = Un-Selected by default
      "LG Keyboard XT9",	  "<~debloatcustomize.25>",							0,	  #-- item.2.5 = Un-Selected by default
  
    "<#0f0><~debloatcustomize.30></#>", "",                                   2,  #-- Group 3. key = "item.3.x"
	  "Chrome",             "<~debloatcustomize.31>",                         0,    #-- item.3.1 = Un-Selected by default
	  "Drive",              "<~debloatcustomize.32>",                         0,    #-- item.3.2 = Un-Selected by default
	  "Gmail",              "<~debloatcustomize.33>",                         0,    #-- item.3.3 = Un-Selected by default
	  "Google Play Books",  "<~debloatcustomize.34>",                         0,    #-- item.3.4 = Un-Selected by default
	  "Google Play Games",  "<~debloatcustomize.35>",                         0,    #-- item.3.5 = Un-Selected by default
	  "Google Play Movies", "<~debloatcustomize.36>",                         0,    #-- item.3.6 = Un-Selected by default
	  "Google Play Music",  "<~debloatcustomize.37>",       				   	0,    #-- item.3.7 = Un-Selected by default
	  "Google Play Newsstand", "<~debloatcustomize.38>",                      0,    #-- item.3.8 = Un-Selected by default
	  "Google Text-to-speech Engine", "<~debloatcustomize.39>",				0,    #-- item.3.9 = Un-Selected by default
	  "Google+",            "<~debloatcustomize.310>",                        0,    #-- item.3.9 = Un-Selected by default
	  "Hangouts", 	      "<~debloatcustomize.311>",                        0,    #-- item.3.10 = Un-Selected by default
	  "Maps",               "<~debloatcustomize.312>",                        0,    #-- item.3.11 = Un-Selected by default
	  "Street View",        "<~debloatcustomize.313>",                        0,    #-- item.3.12 = Un-Selected by default
	  "TalkBack", 	      "<~debloatcustomize.314>",                        0,    #-- item.3.13 = Un-Selected by default
      "YouTube",			  "<~debloatcustomize.315>",						0,    #-- item.3.14 = Un-Selected by default

    "<#0000ff><~debloatcustomize.40></#>", "",  								2,  #-- Group 4. key = "item.4.x"
	  "App Updates",        "<~debloatcustomize.41>",       					0,    #-- item.4.1 = Un-Selected by default
	  "Google Search",	  "<~debloatcustomize.42>",							0,    #-- item.4.2 = Un-Selected by default
	  "LGInstallService",   "<~debloatcustomize.43>",							0,    #-- item.4.3 = Un-Selected by default
	  "Software Update",	  "<~debloatcustomize.44>",				   			0,    #-- item.4.4 = Un-Selected by default
	  "Update Center",	  "<~debloatcustomize.45>",							0,	  #-- item.4.5 = Un-Selected by default

    "<#0f0><~debloatcustomize.50></#>", "",	                				2,  #-- Group 5. key = "item.5.x"
      "Boot Animation",	  "<~debloatcustomize.51>",                     	0,    #-- item.5.1 = Un-Selected by default
	  "Shutdown Animation", "<~debloatcustomize.52>",                         0,    #-- item.5.2 = Un-Selected by default
	  "Boot Sound",		  "<~debloatcustomize.53>",							0,    #-- item.5.3 = Un-Selected by default
	  "Shutdown Sound",	  "<~debloatcustomize.54>",							0,    #-- item.5.4 = Un-Selected by default
	  "Stock music",	      "<~debloatcustomize.55>",							0     #-- item.5.5 = Un-Selected by default

    #--------[ Initial Value = 0: Unselected, 1: Selected, 2: Group Item, 3: Not Visible ]---------#
  );


  ##
  #
  # Next Screen is Installation UI, So change the next button text to "Install Now"
  #
  ini_set("text_next", "<~debloatcustomize.nextbutton>");
  ini_set("icon_next", "@installbutton");

  ###################################################################################################################
  #
  # DEBLOATER STEP 3 - MAKE SURE USER KNOW THAT NEXT STEP WAS INSTALLATION PROCESS
  #
  viewbox(
    #-- Title
      "<~debloatconfirmation.title>",

    #-- Text
      "<~debloatconfirmation.text>",

    #-- Icon
      "@install"
  );

  ##
  #
  # Next Button in successfull installation should be back to "Next >"
  #
  ini_set("text_next", "<~text_next>");
  ini_set("icon_next", "@next");

  #-- Extract sleep to /tmp/aroma/sleep. It will be used in updater-script for testing wait script
  restotmp("exec_demo/sleep","sleep");

  ###################################################################################################################
  #
  # DEBLOATER STEP 4 - INSTALLATION PROCESS
  #
  setvar("retstatus",
    install(
	  #-- Title
        "<~debloatinstall.title>",
	
	  #-- Installation Process message
	    "<b>"+ini_get("rom_name")+"</b> <~debloatinstall.desc>",
	
	  #-- Installation Icon
        "@install",
	
	  #-- Installation Finish Message
        "<~debloatinstall.finish>"
    )
  );

  ###################################################################################################################
  #
  # DEBLOATER STEP 5 - ASK If We Want to go back to main menu
  #
  checkviewbox(
    #-- Title
      "<~debloatpostinstall.title>",
  
    #-- Text
      "<~debloatpostinstall.text>",
	
    #-- Icon
      "@welcome",

    #-- Checkbox Text
      "<~debloatpostinstall.checkbox>",

    #-- Initial Checkbox value ( 0=unchecked, 1=checked ) -  (Optional, default:0)
      "0",

    #-- Save checked value in variable "reboot_it" (Optional)
      "goto_mainmenu"
  );
  if
    getvar("goto_mainmenu")=="1"
  then
    goto("aroma_installer_demo_pos");
  endif;

endif;
##########
#
# END DEBLOATER
#



##
#
# Next Button is "Finish"
#
ini_set("text_next", "<~text_finish>");
ini_set("icon_next", "@finish");

#########################################################################################################
#
# STEP 6 - FINISH MESSAGE, Add Checkbox to Reboot or No. Save in "reboot_it" variable
#
# You can use return value or "reboot_it" variable to retrive checkbox value
# Example:
# if (checkviewbox(....)=="1" then
#    ... checked ...
# endif;
#
checkviewbox(
  #-- Title
    "<~finished.title>",
  
  #-- Text
    "<~finished.text1>\n\n<b>" +ini_get("rom_name")+ "<~finished.text2>\n\n"+
    "<~finished.installer>: "+getvar("retstatus")+"\n\n",
	
  #-- Icon
    "@welcome",

  #-- Checkbox Text
    "<~finished.reboot>",

  #-- Initial Checkbox value ( 0=unchecked, 1=checked ) -  (Optional, default:0)
    "1",

  #-- Save checked value in variable "reboot_it" (Optional)
    "reboot_it"
);

###
#
# Check if reboot checkbox was checked
if
  getvar("reboot_it")=="1"
then
  #
  # reboot("onfinish");   - Reboot if anything finished
  # reboot("now");        - Reboot Directly
  # reboot("disable");    - If you set reboot("onfinish") before, use this command to revert it.
  #
  reboot("onfinish");
endif;

#---- FINISH
